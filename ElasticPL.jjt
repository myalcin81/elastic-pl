
options {
    MULTI=true;
    STATIC=false;
    NODE_EXTENDS="MyNode";
    TRACK_TOKENS=true;
}

PARSER_BEGIN(ElasticPLParser)

public class ElasticPLParser {


  public Node rootNode() {
    return jjtree.rootNode();
  }  

}

PARSER_END(ElasticPLParser)


SKIP : /* WHITE SPACE */
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL: (<DIGIT>)+ >
}


void CompilationUnit() :
{
   String name;
}
{
   InputSpecificationStatement()
   (
       Statement()
   )*
   VerifyStatement()
   <EOF>
}



void Expression() #void:
{}
{
  LOOKAHEAD( Id() "=" )
  Assignment()
|
  ConditionalOrExpression()
}



void Assignment():
{ Token t; }
{
   Id() "=" AdditiveExpression()
}

void ConditionalOrExpression() #void :
{}
{
  ConditionalAndExpression()
  ( "||" ConditionalAndExpression() #OrNode(2) )*
}

void ConditionalAndExpression() #void :
{}
{
  InclusiveOrExpression()
  ( "&&" InclusiveOrExpression() #AndNode(2) )*
}

void InclusiveOrExpression() #void :
{}
{
  ExclusiveOrExpression()
  ( "|" ExclusiveOrExpression() #BitwiseOrNode(2) )*
}

void ExclusiveOrExpression() #void :
{}
{
  AndExpression()
  ( "^" AndExpression() #BitwiseXorNode(2) )*
}

void AndExpression() #void :
{}
{
  EqualityExpression()
  ( "&" EqualityExpression() #BitwiseAndNode(2) )*
}

void EqualityExpression() #void :
{}
{
  RelationalExpression()
  (
     "==" RelationalExpression() #EQNode(2)
   |
     "!=" RelationalExpression() #NENode(2)
  )*
}

void RelationalExpression() #void :
{}
{
  AdditiveExpression()
  (
    "<" AdditiveExpression() #LTNode(2)
   |
    ">" AdditiveExpression() #GTNode(2)
   |
    "<=" AdditiveExpression() #LENode(2)
   |
    ">=" AdditiveExpression() #GENode(2)
  )*
}

void AdditiveExpression() #void :
{}
{
  MultiplicativeExpression()
  (
    "+" MultiplicativeExpression() #AddNode(2)
   |
    "-" MultiplicativeExpression() #SubtractNode(2)
  )*
}

void MultiplicativeExpression() #void :
{}
{
  UnaryExpression()
  (
    "*" UnaryExpression() #MulNode(2)
   |
    "/" UnaryExpression() #DivNode(2)
   |
    "%" UnaryExpression() #ModNode(2)
   |
    "<<" UnaryExpression() #LShiftNode(2)
   |
    ">>" UnaryExpression() #RShiftNode(2)
   |
    ">>>" UnaryExpression() #RRotNode(2)
   |
    "<<<" UnaryExpression() #LRotNode(2)
   |
    "xor" UnaryExpression() #BitwiseXorNode(2)
   |
    "and" UnaryExpression() #BitwiseAndNode(2)
   |
    "or" UnaryExpression() #BitwiseOrNode(2)
  )*
}

void UnaryExpression() #void :
{}
{
  "~" UnaryExpression() #BitwiseComplNode(1)
|
  "!" UnaryExpression() #NotNode(1)
|
  PrimaryExpression()
}

void PrimaryExpression() #void :
{
   String name;
}
{
  Literal()
|
  Id() 
|
  "(" Expression() ")"
}

void Id():
{}
{
  "m[" AdditiveExpression() "]"
}

void Literal() #void :
{
   Token t;
}
{
 (
  t=<INTEGER_LITERAL>
    {
       jjtThis.val = Integer.parseInt(t.image);
    }
 )#IntConstNode
}


void BooleanLiteral() #void :
{}
{
  "true" #TrueNode
|
  "false" #FalseNode
}

void Statement() #void :
{}
{
  ";"
|
  Block()
|
  StatementExpression()
|
  IfStatement()
|
  RepeatStatement()
|
  SHA256()
| 
  MD5()
| 
  RIPEMD160()
| 
  RIPEMD128()
| 
  SHA512()
| 
  Tiger()
| 
  Whirlpool()
|
  SECP256K1PrivToPub()
|
  SECP256K1PointAdd()
|
  SECP256K1PointSub()
|
  SECP256K1PointScalarMult()
| 
  SECP256K1PointNegate()
}


void Block() :
{}
{
  "{" ( Statement() )* "}"
}

void StatementExpression() :
{}
{
  Assignment() ";"
}

void IfStatement() :
{}
{
  "if" "(" Expression() ")" Statement() [ LOOKAHEAD(1) "else" Statement() ]
}

void RepeatStatement() #RepeatStatement(2):
{}
{
  "repeat" "(" Literal() ")" Statement()
}

void InputSpecificationStatement() #InputSpecificationStatement(1):
{}
{
  "input" Literal() ";"
}

void SHA256() #SHA256(2):
{}
{
  "SHA256" Literal() Literal() ";"
}
void SHA512() #SHA512(2):
{}
{
  "SHA512" Literal() Literal() ";"
}
void Whirlpool() #Whirlpool(2):
{}
{
  "Whirlpool" Literal() Literal() ";"
}
void MD5() #MD5(2):
{}
{
  "MD5" Literal() Literal() ";"
}

void SECP256K1PrivToPub() #SECP256K1PrivToPub(2):
{}
{
  "SECP256K1PrivToPub" Literal() BooleanLiteral() ";"
}

void SECP256K1PointAdd() #SECP256K1PointAdd(5):
{}
{
  "SECP256K1PointAdd" Literal() BooleanLiteral() Literal() BooleanLiteral() BooleanLiteral() ";"
}

void SECP256K1PointSub() #SECP256K1PointSub(5):
{}
{
  "SECP256K1PointSub" Literal() BooleanLiteral() Literal() BooleanLiteral() BooleanLiteral() ";"
}
void SECP256K1PointScalarMult() #SECP256K1PointScalarMult(5):
{}
{
  "SECP256K1PointScalarMult" Literal() BooleanLiteral() Literal() Literal() BooleanLiteral() ";"
}

void SECP256K1PointNegate() #SECP256K1PointNegate(3):
{}
{
  "SECP256K1PointNegate" Literal() BooleanLiteral() BooleanLiteral() ";"
}

void Tiger() #Tiger(2):
{}
{
  "Tiger" Literal() Literal() ";"
}
void RIPEMD160() #RIPEMD160(2):
{}
{
  "RIPEMD160" Literal() Literal() ";"
}
void RIPEMD128() #RIPEMD128(2):
{}
{
  "RIPEMD128" Literal() Literal() ";"
}

void VerifyStatement() :
{}
{
  "verify" ConditionalOrExpression() ";"
}


TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER: [ "a"-"z", "A"-"Z" ] >
|
  < #DIGIT: [ "0"-"9"] >
|
  < #DIGIT_FROM_ONE: [ "1"-"9"] >
|
  < #DIGIT_ONE_TO_SIX: [ "1"-"6"] > 
}
